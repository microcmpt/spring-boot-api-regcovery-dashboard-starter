group 'com.github.microcmpt'
version '1.0.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url aliyunMavenUrl }
    maven { url snapshotsMaven }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
    compile ('org.springframework.boot:spring-boot-starter-jersey:1.5.10.RELEASE') {
        exclude(module: 'spring-boot-starter-tomcat')
        exclude(module: 'spring-boot-starter-validation')
    }
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile(group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.4.10') {
        exclude(module: 'slf4j-log4j12')
    }
    compile group: 'com.101tec', name: 'zkclient', version: '0.10'
}

uploadArchives {
    repositories {
        mavenDeployer {
            //为Pom文件做数字签名
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('ossrhUsername'), password: System.getenv('ossrhPassword'))
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv('sonatypeUsername'), password: System.getenv('sonatypePassword'))
            }
            pom.project {
                name project.name
                packaging 'jar'
                description 'Lightweight RPC communication framework based on netty and protostuff.'
                url 'https://github.com/microcmpt/msa-api-regcovery'

                scm {
                    url 'https://github.com/microcmpt/msa-rpc4j'
                    connection 'git@github.com:microcmpt/msa-rpc4j.git'
                    developerConnection 'git@github.com:microcmpt/msa-rpc4j.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'xiapshen'
                        name 'Shen Xia ping'
                        email '1378127237@qq.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}